//This Behaviour Tree controls the 'Patroller' unit.

tree("BeAlive")
	//This unit attacks when possible,
	//otherwise it patrols a predefined path.
	fallback
		tree("Attack")
		tree("Patroll")


tree("Patroll")
	//While no enemy is spotted,
	//follow the assigned waypoints.
	while
		sequence
			not HasEnemy
			not Acquire_Enemy

		repeat
			sequence
				SetDestination_Waypoint
				SetTarget_Destination
				AimAt_Target
				MoveTo_Destination
				Wait(0.3)
				NextWaypoint



tree("Attack")
	//When the enemy is visible, attack it,
	//Otherwise forget about it.
	fallback

		//Attack the enemy if visible.
		repeat
			sequence
				IsVisible_Enemy
				Stop
				Wait(0.5)
				SetTarget_Enemy
				AimAt_Target
				tree("Fire")

		//Otherwise forget about it.
		sequence
			Clear_Enemy
			Fail

