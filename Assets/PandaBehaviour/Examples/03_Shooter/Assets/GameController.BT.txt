//This Behaviour controls the game.

tree("Root")
	parallel
		sequence
			//Indicates that the game is starting
			//by blinking a text.
			DebugLog("GAME START")
			repeat(3)
				sequence
					Display("GAME START")
					Wait(0.5)
					Display("")
					Wait(0.5)

		repeat
			//The game finishes either on Game Over or
			//when the level is completed.
			fallback

				//It's Game over when the player is dead.
				sequence
					IsPlayerDead
					DebugLog("GAME OVER")
					Wait(2.0)
					Display("GAME OVER")
					Wait(3.0)
					ReloadLevel

				//The level is completed when all enemies are dead.
				sequence
					IsLevelCompleted
					Display("LEVEL COMPLETED")
					DebugLog("LEVEL COMPLETED")
					Wait(3.0)
					ReloadLevel

				//Just succeed if nothing happen.
				Succeed


